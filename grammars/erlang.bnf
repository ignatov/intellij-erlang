// http://erlang.org/pipermail/erlang-questions/2011-November/062390.html
// http://erlang.org/pipermail/erlang-questions/attachments/20111114/ba08f89c/attachment.obj
// https://github.com/erlang/otp/blob/master/lib/stdlib/src/erl_parse.yrl
//
// Erlang bnf grammar
//
// Rootsymbol form

{
  parserClass="org.intellij.erlang.parser.ErlangParser"
  stubParserClass="org.intellij.erlang.parser.ErlangParserUtil"

  implements="org.intellij.erlang.psi.ErlangCompositeElement"
  extends="org.intellij.erlang.psi.impl.ErlangCompositeElementImpl"

  elementTypeHolderClass="org.intellij.erlang.ErlangTypes"
  elementTypePrefix="ERL_"
  elementTypeClass="org.intellij.erlang.psi.ErlangCompositeElementType"
  tokenTypeClass="org.intellij.erlang.psi.ErlangTokenType"

  psiClassPrefix="Erlang"
  psiImplClassSuffix="Impl"
  psiPackage="org.intellij.erlang.psi"
  psiImplPackage="org.intellij.erlang.psi.impl"

  psiImplUtilClass="org.intellij.erlang.psi.impl.ErlangPsiImplUtil"

  // operators
  OP_PLUS         = "+"
  OP_MINUS        = "-"
  OP_AR_MUL       = "*"
  OP_AR_DIV       = "/"
  OP_DIV          = "div"
  OP_REM          = "rem"
  OP_OR           = "or"
  OP_XOR          = "xor"
  OP_BOR          = "bor"
  OP_BXOR         = "bxor"
  OP_BSL          = "bsl"
  OP_BSR          = "bsr"
  OP_AND          = "and"
  OP_BAND         = "band"
  OP_EQ_EQ        = "=="
  OP_DIV_EQ       = "/="
  OP_EQ_COL_EQ    = "=:="
  OP_EQ_DIV_EQ    = "=/="
  OP_LT           = "<"
  OP_EQ_LT        = "=<"
  OP_GT           = ">"
  OP_GT_EQ        = ">="
  OP_LT_EQ        = "<="
  OP_NOT          = "not"
  OP_BNOT         = "bnot"
  OP_PLUS_PLUS    = "++"
  OP_MINUS_MINUS  = "--"
  OP_EQ           = "="
  OP_EXL          = "!"
  OP_LT_MINUS     = "<-"
  OP_ANDALSO      = "andalso"
  OP_ORELSE       = "orelse"

  BIN_START       = "<<"
  BIN_END         = ">>"

  PAR_LEFT        = "("
  PAR_RIGHT       = ")"
  CURLY_LEFT      = "{"
  CURLY_RIGHT     = "}"
  BRACKET_LEFT    = "["
  BRACKET_RIGHT   = "]"
  DOT             = "."
  DOT_DOT         = ".."
  DOT_DOT_DOT     = "..."
  COLON           = ":"
  COLON_COLON     = "::"
  OR_OR           = "||"
  OR              = "|"
  SEMI            = ";"
  COMMA           = ","
  ARROW           = "->"
  RADIX           = "#"
  QMARK           = "?"
  UNI_PATTERN     = "_"
  
  AFTER           = "after"
  COND            = "cond"
  LET             = "let"
  WHEN            = "when"
  BEGIN           = "begin"
  END             = "end"
  OF              = "of"
  CASE            = "case"
  FUN             = "fun"
  QUERY           = "query"
  CATCH           = "catch"
  IF              = "if"
  RECEIVE         = "receive"
  TRY             = "try"

  INT             = "integer"
  FLOAT           = "float"
  CHAR            = "char"
  
  pin(".*_expression")=1
  
  extends("assignment_expression")=fake_binary_expression
  extends(".*_(expression|comprehension|literal|a)")=expression
  name(".*_(expression|comprehension|literal|a)")=expression
  name(".*attribute")=attribute
  name(".*type|.*_t")=type
  extends("atomic")=expression

  implements("record_definition|function|q_var|module|macros_definition")="org.intellij.erlang.psi.ErlangNamedElement"
  extends("record_definition|function|q_var|module|macros_definition")="org.intellij.erlang.psi.impl.ErlangNamedElementImpl"
}

forms ::= form ('.' form)*
private form ::= function | rule | record_definition | include | macros_definition | attribute {recoverUntil=recoverer}

q_atom ::= macros | atom
q_var ::= '_' | var {
  methods = [getReference processDeclarations getName setName getNameIdentifier]
}
macros ::= '?' macros_name {
  pin=1
  methods = [getReference]
}
macros_name ::= atom | var

private recoverer ::= !('.')

record_definition ::= '-' 'record' '(' q_atom ',' typed_record_fields ')' {
  pin=2
  methods = [getName setName getNameIdentifier getTextOffset]
}

macros_definition ::= '-' 'define' '(' macros_name ['(' q_atom_or_var (',' q_atom_or_var)* ')']',' expression ')' {
  pin = 2
  methods = [getName setName getNameIdentifier getTextOffset]
}

include ::= '-' ('include' | 'include_lib') '(' include_string ')' {pin=2}
include_string ::= string {methods = [getReference]}

attribute ::= '-' (
  module
  | export
  | specification
  | callback_spec
  | atom_attribute
  ) {pin=1}
callback_spec ::= 'callback' type_spec {pin=1}
specification ::= 'spec' type_spec {pin=1}
export ::= 'export' '(' export_functions ')' {pin=1}
module ::= 'module' '(' q_atom (',' argument_definition)? ')' {
  pin=1
  methods = [getName setName getNameIdentifier getTextOffset processDeclarations]
}

atom_attribute ::= q_atom [('(' typed_attr_val ')') | typed_attr_val | attr_val]
private export_functions ::= '[' [export_function (',' export_function)*] ']' {pin=1}
export_function ::= q_atom '/' integer {
  pin= 1
  methods = [getReference]
}

type_spec ::= ('(' spec_fun type_sigs ')') | spec_fun type_sigs
spec_fun ::= q_atom [('/' integer '::') | (':' q_atom ['/' integer '::'])]
typed_attr_val ::= expression ((',' typed_record_fields) | ('::' top_type))
typed_record_fields ::= '{' typed_exprs? '}' {pin=1}
private typed_exprs ::= typed_expr (',' typed_expr)*
typed_expr ::= expression ['::' top_type]
private type_sigs ::= type_sig (';' type_sig)*
type_sig ::= fun_type [when type_guards]
private type_guards ::= type_guard (',' type_guard)*
type_guard ::= (q_atom '(' top_types ')') | top_type
private top_types ::= top_type (',' top_type)*
top_type ::= [q_var '::'] top_type_100_t
top_type_100_t ::= type ['|' top_type_100_t]
type ::=
  ('(' top_type ')')
  | int_type
  | (int_type '..' int_type)
  | (fun '(' fun_type_100_t? ')')
  | [q_atom ':'] q_atom ['(' top_types? ')']
  | ( q_atom '(' top_types ')')
  | binary_type
  | q_var
  | ('[' top_type (',' '...')? ']')
  | ('{' top_types? '}')
  | ('#' q_atom '{' field_types? '}')

int_type ::= '-'? integer
fun_type_100_t ::= '(' ('...' | top_types?) ')' '->' top_type
fun_type ::= '(' top_types? ')' '->' top_type
private field_types ::= field_type (',' field_type)*
field_type ::= q_atom '::' top_type

binary_type ::= '<<' [bin_base_type | bin_unit_type | bin_base_types] '>>' {pin=1}
private bin_base_types ::= bin_base_type ',' bin_unit_type
bin_base_type ::= q_var ':' integer
bin_unit_type ::= q_var ':' q_var '*' integer

attr_val ::= ('(' exprs ')') | exprs {name="attribute value"}

function ::= function_clause (';' function_clause)* {
  methods = [
    atom_name = "function_clause[0]/q_atom"
    getName
    setName
    getArity
    getNameIdentifier
  ]
}

function_clause ::= q_atom argument_definition_list clause_guard? clause_body
private argument_definition_list ::= '(' argument_definition? (',' argument_definition)* ')'

clause_guard ::= when guard {pin=1}
clause_body ::= '->' exprs {pin=1}

expression ::= q_atom_or_var&(',' | ')' | '->' | '}' | ']' | ';') | catch_expression | expr_100_a
catch_expression ::= catch expression
expr_100_a ::= expr_150_a [assignment_expression|send_expression]
left assignment_expression ::= '=' expr_100_a
left send_expression ::= '!' expr_100_a
expr_150_a ::= expr_160_a [orelse expr_150_a]
expr_160_a ::= expr_200_a [andalso expr_160_a]
expr_200_a ::= expr_300_a [comp_op expr_300_a]
expr_300_a ::= expr_400_a [list_op expr_300_a]
expr_400_a ::= expr_500_a additive_expression*
left additive_expression ::= add_op expr_500_a {pin=1}
expr_500_a ::= prefix_expression multiplicative_expression*
left multiplicative_expression ::= mult_op prefix_expression {pin=1}
prefix_expression ::= prefix_op? expr_700_a {pin=0}
expr_700_a ::= global_function_call_expression | function_call_expression | generic_function_call_expression |
  anonymous_call_expression | record_expression+ | colon_qualified_expression
colon_qualified_expression ::= (max_expression | qualified_reference) colon_max_expression? {pin=0}
private colon_max_expression ::= ':' max_expression
private qualified_reference ::= q_atom qualified_expression*
left qualified_expression ::= '.' q_atom {pin=0} //todo

max_expression ::=
    atomic
  | q_var
  | tuple_expression
  | list_expression
  | case_expression
  | if_expression
  | list_comprehension
  | receive_expression
  | fun_expression
  | try_expression
  | query_expression
  | parenthesized_expression
  | binary_expression
  | begin_end_expression

begin_end_expression ::= begin begin_end_body end
begin_end_body ::= exprs {name="expression"}
parenthesized_expression ::= '(' expression ')'

//list_expression ::= '[' expression? (',' expression)* ['|' expression] ']' {pin=0}

list_expression ::= '[' (']' | (expression tail)) {pin=0} // todo: rewrite without tail
private tail ::= ']' | ('|' expression ']') | (',' expression tail)

binary_expression ::= '<<' bin_elements? '>>' {pin=1}
private bin_elements ::= bin_element (',' bin_element)*
bin_element ::= prefix_op? max_expression (colon_max_expression)? opt_bit_type_list? {name="binary element"}

opt_bit_type_list ::= '/' bit_type_list {pin=1}
private bit_type_list ::= bit_type ('-' bit_type)*
bit_type ::= q_atom [':' integer]

list_comprehension ::= '[' expression '||' lc_exprs ']' {
  pin=3
  methods=[processDeclarations]
}
lc_exprs ::= lc_expression (',' lc_expression)*

lc_expression ::= (argument_definition (('<-' | '<=') expression)?) {pin=0}

tuple_expression ::= '{' exprs? '}' {pin=1}

record_expression ::= max_expression? '#' (q_atom | q_var) (('.' q_atom) | record_tuple) {
  pin=2
  methods = [
    atom_name = "q_atom[0]"
    getReference
  ]
}

record_tuple ::= '{' record_fields? '}' {pin=1}
private record_fields ::= record_field (',' record_field)*
record_field ::= q_atom_or_var '=' (qualified_atom_expression | expression)
private qualified_atom_expression ::= q_atom '.' q_atom {pin=0}
module_ref ::= q_atom {
  methods = [getReference]
}
global_function_call_expression ::= module_ref ':' function_call_expression {pin=0}
function_call_expression ::= q_atom argument_list {
  pin=0
  methods = [getReference getNameIdentifier getTextOffset]
}
generic_function_call_expression ::= (q_atom_or_var ':')? q_atom_or_var argument_list {pin=0}
private q_atom_or_var ::= q_atom | q_var

private anonymous_call_expression ::= (fun_expression|parenthesized_expression) argument_list {pin=0}

if_expression ::= if if_clauses end
if_clauses ::= if_clause (';' if_clause)*
if_clause ::= guard clause_body

case_expression ::= case expression of cr_clauses end
cr_clauses ::= cr_clause (';' cr_clause)*
cr_clause ::= argument_definition clause_guard? clause_body
argument_definition ::= expression

receive_expression ::= receive ((cr_clauses end) | (cr_clauses? after expression clause_body end))
fun_expression ::= fun ((fun_clauses end) | ([module_ref ':'] function_with_arity))
function_with_arity ::= q_atom '/' integer {
  pin= 1
  methods = [getReference]
}
private fun_clauses ::= fun_clause (';' fun_clause)*
fun_clause ::= argument_definition_list clause_guard? clause_body
try_expression ::= try exprs (of cr_clauses)? try_catch
try_catch ::= (catch try_clauses [after exprs] end) | (after exprs end) {pin(".*")="catch|after"}
try_clauses ::= try_clause (';' try_clause)*
try_clause ::= [argument_definition ':'] argument_definition clause_guard? clause_body

query_expression ::= query list_comprehension end
argument_list ::= '(' exprs? ')'

fake fake_binary_expression ::= expression + {
  methods = [
    left="expression[0]"
    right="expression[1]"]
  }

private exprs ::= expression (',' expression)*
guard ::= exprs (';' exprs)*
private atomic ::= char | integer | float | q_atom | string+

private prefix_op ::= '+' | '-' | bnot | not
private mult_op ::= '/' |'*' | div | rem  | band | and
private add_op ::= '+' |'-' | bor | bxor | bsl | bsr | or | xor
private list_op ::= '++' | '--'
private comp_op ::= '==' | '/=' |'=<' |'<' |'>=' |'>' |'=:=' |'=/='

rule ::= rule_clause (';' rule_clause)*
rule_clause ::= q_atom argument_list clause_guard? rule_body
rule_body ::= ':-' lc_exprs {pin=1}